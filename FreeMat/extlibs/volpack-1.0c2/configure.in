dnl  This file is an input file used by the GNU "autoconf" program to
dnl  generate the file "configure", which is run during installation
dnl  to configure the system for the local environment.

AC_INIT(src/vp_global.h)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(volpack, 1.0c2)

MAJOR=1
MINOR=4 
LIBVERSION=${MAJOR}:${MINOR}:0

AC_CANONICAL_HOST

dnl make substitutions 
dnl AC_SUBST(OFLAGS)
dnl AC_SUBST(MFLAGS)
AC_SUBST(MAJOR)
AC_SUBST(MINOR)
AC_SUBST(LIBVERSION)

AC_PROG_INSTALL
AC_PROG_CC
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL()


AC_MSG_CHECKING([for native Win32])
case "$host" in
  *-*-mingw*)
    native_win32=yes
    ;;
  *)
    native_win32=no
    ;;
esac
AC_MSG_RESULT([$native_win32])
AM_CONDITIONAL(OS_WIN32, test "$native_win32" = yes)

AC_MSG_CHECKING([for Win32 platform in general])
case "$host" in
  *-*-mingw*|*-*-cygwin*)
    platform_win32=yes
    ;;
  *)
    platform_win32=no
    ;;
esac
AC_MSG_RESULT($platform_win32)
AM_CONDITIONAL(PLATFORM_WIN32, test "$platform_win32" = yes)

# Ensure MSVC-compatible struct packing convention is used when
# compiling for Win32 with gcc. GTK+ uses this convention, so we must, too.
# What flag to depends on gcc version: gcc3 uses "-mms-bitfields", while
# gcc2 uses "-fnative-struct".
if test x"$native_win32" = xyes; then
  if test x"$GCC" = xyes; then
    msnative_struct=''
    AC_MSG_CHECKING([how to get MSVC-compatible struct packing])
    if test -z "$ac_cv_prog_CC"; then
      our_gcc="$CC"
    else
      our_gcc="$ac_cv_prog_CC"
    fi
    case `$our_gcc --version | sed -e 's,\..*,.,' -e q` in
      2.)
	if $our_gcc -v --help 2>/dev/null | grep fnative-struct >/dev/null; then
	  msnative_struct='-fnative-struct'
	fi
	;;
      *)
	if $our_gcc -v --help 2>/dev/null | grep ms-bitfields >/dev/null; then
	  msnative_struct='-mms-bitfields'
	fi
	;;
    esac
    if test x"$msnative_struct" = x ; then
      AC_MSG_RESULT([no way])
      AC_MSG_WARN([produced libraries will be incompatible with prebuilt GTK+ DLLs])
    else
      CFLAGS="$CFLAGS $msnative_struct"
      AC_MSG_RESULT([${msnative_struct}])
    fi
  fi
fi


if test -z "$M4" ; then
  case "$host" in
  *-sunos*)
    AC_CHECKING(/usr/5bin/m4)
    if test -f /usr/5bin/m4; then
      M4="/usr/5bin/m4"
    fi
    ;;
  esac
fi

AC_PATH_PROGS(M4, gnum4 gm4 m4, m4)
AC_HEADER_STDC
AC_CHECK_HEADERS(string.h memory.h)

AC_FUNC_VPRINTF


AC_OUTPUT([
	Makefile
	src/Makefile 
	man/Makefile
	doc/Makefile
	examples/Makefile
	volpack.spec
	volpack-$VERSION-1.info:volpack.info.in
])

