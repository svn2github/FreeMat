dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/x11/main.cpp)

dnl sets build, host, target variables and the same with _alias
AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE(FreeMat, 1.0)

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_F77
AM_PROG_LEX
AC_PROG_YACC
AC_PROG_RANLIB

case $host in
*-*-linux-* | *-*-cygwin*)
  FLIBS=`echo $FLIBS | sed -e 's/-lfrtbegin//g'` ;;
esac

maindir="src/x11"
case $host in
*-*-cygwin*)
  maindir="src/win32"
esac

AC_PATH_XTRA

AC_ARG_WITH(debug,
	[AC_HELP_STRING([--with-debug],
		[Include debug options [default=no]])],
		debug_flag="-O0 -g3",
		debug_flag="-O3")

AC_ARG_WITH(miniblas,
	[AC_HELP_STRING([--with-miniblas],
		[Force use of built-in BLAS library [default=no]])],
		forceminiblas="yes",
		forceminiblas="no")

AC_ARG_WITH(minilapack,
	[AC_HELP_STRING([--with-minilapack],
		[Force use of built-in LAPACK library [default=no]])],
		forceminilapack="yes",
		forceminilapack="no")

AC_ARG_WITH(mpi,
	[AC_HELP_STRING([--with-mpi],
	        [Build the MPI executables [default=no]])],
		usempi="yes",
		usempi="no")

MP_WITH_CURSES([],[])

case $host in
*-*-cygwin*)
  CURSES_LIB=""
esac

LIBS="$debug_flag $prof_flag $LIBS"
CXXFLAGS="$debug_flag $prof_flag"
YFLAGS="$YFLAGS -d"
LFLAGS="$LFLAGS"

AC_SUBST(YFLAGS)
AC_SUBST(LFLAGS)

#ACX_LAPACK([] , AC_MSG_ERROR([Could not find LAPACK+BLAS.  See installation instructions for more information...]) )
AC_LANG_CPLUSPLUS
ACX_MPI([], usempi="no")
AC_CHECK_LIB(png, png_check_sig, LIBS="-lpng -lz -lm $LIBS"; AC_DEFINE(HAVE_PNG,1,[Define if you have PNG library.]), , [-lz -lm])
AC_CHECK_LIB(jpeg, jpeg_read_header, LIBS="-ljpeg $LIBS"; AC_DEFINE(HAVE_JPEG,1,[Define if you have JPEG library.]))
AC_CHECK_LIB(tiff, TIFFError, LIBS="-ltiff -lm $LIBS";AC_DEFINE(HAVE_TIFF,1,[Define if you have TIFF library.]),  , [-lm])

if test "$forceminiblas" = "yes" ; then
  BLAS_LIBS="-L\${top_builddir}/libs/libMiniBLAS -lMiniBLAS"
  miniblasdir="libMiniBLAS"
fi

if test "$forceminilapack" = "yes" ; then
  LAPACK_LIBS="-L\${top_builddir}/libs/libMiniLAPACK -lMiniLAPACK"
  minilapackdir="libMiniLAPACK"
fi

if test "$usempi" = "yes" ; then
  mpidir="mpi"
fi

AC_SUBST(minilapackdir)
AC_SUBST(miniblasdir)
AC_SUBST(mpidir)
AC_SUBST(MPICXX)
AC_SUBST(maindir)

if test "$forceminiblas" = "yes" ; then
   AC_OUTPUT(libs/libMiniBLAS/Makefile)
else
   ACX_BLAS([], AC_MSG_ERROR([Could not find BLAS. Try using --with-blas or --with-miniblas.]))
fi

if test "$forceminilapack" = "yes" ; then
   AC_OUTPUT(libs/libMiniLAPACK/Makefile)
else
   ACX_LAPACK([], AC_MSG_ERROR([Could not find LAPACK. Try using --with-lapack or --with-lapack.]))
fi

LIBS="$LAPACK_LIBS $BLAS_LIBS $LIBS $FLIBS $CURSES_LIB"

ac_configure_args="--srcdir=$ac_abs_top_srcdir/libs/libffi $ac_configure_args"
AC_CONFIG_SUBDIRS(libs/libffi)

AC_OUTPUT(Makefile src/win32/Makefile src/x11/Makefile doc/Doxyfile libs/Makefile libs/libFFTPack/Makefile libs/libFreeMat/Makefile libs/libCore/Makefile libs/libGraphics/Makefile libs/libXP/Makefile mpi/Makefile)

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:       ${srcdir}
  Main location:              ${maindir}
  C Compiler:	              ${CC}
  C Compiler flags:           ${CFLAGS}
  C++ Compiler:		      ${CXX}
  C++ Compiler flags:         ${CXXFLAGS}
  Host System Type:           ${host}
  Install path:		      ${prefix}
  Libs:			      ${LIBS}
  Force Mini-LAPACK:	      ${forceminilapack}
  Force Mini-BLAS:	      ${forceminiblas}

------------------------------------------------------------------------"

