%{
// Copyright (c) 2002, 2003 Samit Basu
// 
// Permission is hereby granted, free of charge, to any person obtaining a 
// copy of this software and associated documentation files (the "Software"), 
// to deal in the Software without restriction, including without limitation 
// the rights to use, copy, modify, merge, publish, distribute, sublicense, 
// and/or sell copies of the Software, and to permit persons to whom the 
// Software is furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included 
// in all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS 
// OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, 
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL 
// THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER 
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING 
// FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER 
// DEALINGS IN THE SOFTWARE.

    /*************************************************************
     * $Source$
     * $Revision$
     * $Date$
     * $Author$
     *************************************************************/


#include <stdio.h>
#include <stdlib.h>
#include "AST.hpp"
#include "WalkTree.hpp"
#include "FunctionDef.hpp"
#include "ParserInterface.hpp"
#include "Exception.hpp"

#define YYSTYPE FreeMat::ASTPtr

#define MSGBUFLEN 2048
static char msgBuffer[MSGBUFLEN];

#include "LexerInterface.hpp"

//extern char* yytext;
extern int yylex(void);

extern int lineNumber;
extern int yydebug;
extern const char *parsing_filename;

bool interactiveMode;

#ifdef WIN32
#define snprintf _snprintf
#endif

namespace FreeMat {
  static ASTPtr mainAST;
  static MFunctionDef *mainMDef;
  
  bool errorOccured;
  static char *expectString = NULL;

  void yyexpect(char *s) {
	expectString = s;
  }

  void yyerror(char *t) {
    t = NULL;
  }

  int yyreport(char *xStr) {
    char buffer[256];
    int addone = 1;
    if (xStr)
      if (!interactiveMode)
        snprintf(msgBuffer,MSGBUFLEN,
	"Expecting %s\n\tat line %d of file %s",
	xStr,lineNumber+addone,parsing_filename);
      else
        snprintf(msgBuffer,MSGBUFLEN,"Expecting %s",xStr);
    else
      if (!interactiveMode)	
        snprintf(msgBuffer,MSGBUFLEN,
	"Syntax error at line %d of file %s",
	lineNumber+addone,parsing_filename);
      else
        snprintf(msgBuffer,MSGBUFLEN,"Syntax error at input");
    throw Exception(msgBuffer);
    return 0;
  }

  void yyxpt(char *t) {
   yyreport(t);
  }

}

using namespace FreeMat;

%}

%token IDENT
%token NUMERIC ENDQSTMNT ENDSTMNT
%token LE GE EQ
%token DOTTIMES DOTRDIV DOTLDIV
%token DOTPOWER DOTTRANSPOSE
%token STRING SPECIALCALL
%token END IF FUNCTION FOR BREAK
%token MAGICEND
%token WHILE ELSE ELSEIF
%token SWITCH CASE OTHERWISE CONTINUE
%token TRY CATCH
%token FIELD REFLPAREN REFRPAREN
%token PERSISTENT
%token KEYBOARD RETURN VARARGIN VARARGOUT GLOBAL
%token QUIT RETALL

%left ':'
%left '|'
%left '&'
%left '<' LE '>' GE EQ NE
%left '+' '-'
%left '*' '/' '\\' DOTTIMES DOTRDIV DOTLDIV
%left POS NEG NOT
%right '^' DOTPOWER
%nonassoc '\'' DOTTRANSPOSE

%%

program:
  statementList {mainAST = $1;} |
  functionDefList |
;

functionDef:
   FUNCTION  returnDeclaration IDENT '(' argumentList ')' newLine statementList {
     MFunctionDef *r;
     r = new MFunctionDef();
     r->returnVals = $2->toStringList();
     r->name = $3->text;
     r->arguments = $5->toStringList();
     r->code = $8;
     r->nextFunction = NULL;
     if (mainMDef == NULL)
	mainMDef = r;
     else {
	r->localFunction = true;
        r->nextFunction = mainMDef->nextFunction;
	mainMDef->nextFunction = r;
     }
   } |
   FUNCTION  IDENT '(' argumentList ')' newLine statementList {
     MFunctionDef *r;
     r = new MFunctionDef();
     r->name = $2->text;
     r->arguments = $4->toStringList();
     r->code = $7;
     r->nextFunction = NULL;
     if (mainMDef == NULL)
	mainMDef = r;
     else {
	r->localFunction = true;
        r->nextFunction = mainMDef->nextFunction;
	mainMDef->nextFunction = r;
     }
   } |
   FUNCTION  returnDeclaration IDENT newLine statementList {
     MFunctionDef *r;
     r = new MFunctionDef();
     r->returnVals = $2->toStringList();
     r->name = $3->text;
     r->code = $5;
     r->nextFunction = NULL;
     if (mainMDef == NULL)
	mainMDef = r;
     else {
	r->localFunction = true;
        r->nextFunction = mainMDef->nextFunction;
	mainMDef->nextFunction = r;
     }
   } |
   FUNCTION  IDENT newLine statementList {
     MFunctionDef *r;
     r = new MFunctionDef();
     r->name = $2->text;
     r->code = $4;
     r->nextFunction = NULL;
     if (mainMDef == NULL)
	mainMDef = r;
     else {
	r->localFunction = true;
        r->nextFunction = mainMDef->nextFunction;
	mainMDef->nextFunction = r;
     }
   } |
   FUNCTION  returnDeclaration IDENT '(' ')' newLine statementList {
     MFunctionDef *r;
     r = new MFunctionDef();
     r->returnVals = $2->toStringList();
     r->name = $3->text;
     r->code = $7;
     r->nextFunction = NULL;
     if (mainMDef == NULL)
	mainMDef = r;
     else {
	r->localFunction = true;
        r->nextFunction = mainMDef->nextFunction;
	mainMDef->nextFunction = r;
     }
   } |
   FUNCTION  IDENT '(' ')' newLine statementList {
     MFunctionDef *r;
     r = new MFunctionDef();
     r->name = $2->text;
     r->code = $6;
     r->nextFunction = NULL;
     if (mainMDef == NULL)
	mainMDef = r;
     else {
	r->localFunction = true;
        r->nextFunction = mainMDef->nextFunction;
	mainMDef->nextFunction = r;
     }
   } |
   FUNCTION  error {yyxpt("function name or return declaration");} |
   FUNCTION  IDENT error {yyxpt("argument list or statement list");} |
   FUNCTION  IDENT '('  error {yyxpt("(possibly empty) argument list");}  |
   FUNCTION  IDENT '(' argumentList ')' error {yyxpt("statement list");} |
   FUNCTION  returnDeclaration  error {yyxpt("function name");} |
   FUNCTION  returnDeclaration IDENT error {yyxpt("argument list or statement list");} |
   FUNCTION  returnDeclaration IDENT '('  error {yyxpt("(possibly empty) argument list");} |
   FUNCTION  returnDeclaration IDENT '(' argumentList ')' error {yyxpt("statement list");}
  ;

functionDefList:
  functionDef |
  functionDefList functionDef
  ;

returnDeclaration:
  VARARGOUT '=' {$$ = $1;} |
  IDENT '=' {$$ = $1;} | 
  '[' argumentList ']' '=' {$$ = $2;} |
  error {yyxpt("either 'varargout', a single returned variable, or a list of return variables in return declaration");} |
  VARARGOUT error {yyxpt("an '=' symbol after 'varargout' in return declaration");} |
  IDENT error {yyxpt("an '=' symbol after identifier in return declaration");} |
  '[' error {yyxpt("a valid list of return arguments in return declaration");} | 
  '[' argumentList error {yyxpt("matching ']' in return declaration");} |
  '[' argumentList ']' error {yyxpt("an '=' symbol after return declaration");}
  ;

argumentList:
  argument {$$ = $1;}
  | argumentList ',' argument {$$ = $1; $$->addChild($3);}
  ;

argument:
  IDENT | '&' IDENT {
	$$ = $2;
	char *b = (char*) malloc(strlen($2->text)+2);
	b[0] = '&';
	strcpy(b+1,$2->text);
	$$->text = b;
  }
  ;
  

statementList:
   statement {$$ = new AST(OP_BLOCK,$1,$1->context());} | 
   statementList statement {$$ = $1; $$->addChild($2);}
   ;

statement:
	 statementType ENDQSTMNT {
  	    $$ = new AST(OP_QSTATEMENT,NULL,(int)$2);
	    $$->down = $1;
	 }
	 | statementType ENDSTMNT {
	    $$ = new AST(OP_RSTATEMENT,NULL,(int)$2);
            $$->down = $1;
	 }
	 | statementType ',' {
	    $$ = new AST(OP_RSTATEMENT,NULL,(int)$2);
	    $$->down = $1;
	 } 
	 ;

statementType:
	 assignmentStatement
	 | expr
	 | {$$ = new AST(null_node,"",-1);} 
         | multiFunctionCall
         | forStatement
         | breakStatement
	 | continueStatement
         | whileStatement
       	 | ifStatement
	 | switchStatement
	 | tryStatement
	 | keyboardStatement
	 | returnStatement
	 | globalStatement
	 | persistentStatement
	 | specialSyntaxStatement
	 | QUIT | RETALL |
	error {yyxpt("recognizable statement type (assignment, expression, function call, etc...)");}
         ;

specialSyntaxStatement:
	SPECIALCALL stringList {$1->addChild($2); 
				$$ = new AST(OP_SCALL,$1,$1->context());}
	;

stringList:
	STRING {$$ = new AST(OP_PARENS,$1,$1->context());} |
	stringList STRING {$$ = $1; $$->addChild($2);}
	;

persistentStatement:
	PERSISTENT identList {$$ = $1; $$->addChild($2); } |
	PERSISTENT error {yyxpt("list of variables to be tagged as persistent");}
 	;

globalStatement:
	GLOBAL identList {$$ = $1; $$->addChild($2);} |
	GLOBAL error {yyxpt("list of variables to be tagged as global");}
	;

identList:
	IDENT | 
	identList IDENT {$$ = $1; $$->addChild($2);} |
	identList error {yyxpt("list of valid identifiers");}
	;

returnStatement:
	RETURN
	;

keyboardStatement:
	KEYBOARD
	;

continueStatement:
	 CONTINUE
	 ;
breakStatement:
	 BREAK
	 ;

tryStatement:
	TRY statementList optionalCatch END 
	{ $$ = $1; $$->addChild($2); if ($3 != NULL) $$->addChild($3);} 
        ;

optionalCatch:
	CATCH statementList {$$ = $2;}
	| {$$ = NULL;}
	;

switchStatement:
	SWITCH 	expr optionalEndStatement caseBlock otherwiseClause END {
	  $$ = $1; $$->addChild($2); 
	  if ($4 != NULL) $$->addChild($4); 
	  if ($5 != NULL) $$->addChild($5);
	}
        ;

optionalEndStatement:
	',' | ENDSTMNT | ENDQSTMNT | ';' | '#'
	;

newLine:
	ENDSTMNT | ENDQSTMNT
	;


caseBlock:
	/* empty */ {$$ = NULL;} |
	caseList
	;

caseList:
	caseStatement {
	  $$ = new AST(OP_CASEBLOCK,$1,$1->context());
	} |
	caseList caseStatement {
	  $$ = $1; $$->addChild($2);
	}
;

caseStatement:
	CASE expr optionalEndStatement statementList {
	  $$ = $1; $$->addChild($2); $$->addChild($4);
	}
;

otherwiseClause:
	OTHERWISE statementList {
	  $$ = $2;
	} |
	/* empty */ {
	  $$ = NULL;
	}
;

forStatement:
	FOR forIndexExpression optionalEndStatement statementList END {
	  $$ = $1; $$->addChild($2); $$->addChild($4);
	}
;

forIndexExpression:
	'(' IDENT '=' expr ')' {$$ = $2; $$->addChild($4);} |
	IDENT '=' expr {$$ = $1; $$->addChild($3);} |
	IDENT {$$ = $1; 
	      $$->addChild(new AST(OP_RHS,new AST(id_node,$1->text,$1->context()),$1->context())); } |
	'(' IDENT '=' expr error {yyxpt("matching right parenthesis");} |
	'(' IDENT '=' error {yyxpt("indexing expression");} |
	'(' IDENT error {yyxpt("equals operator after loop index");} |
	'(' error {yyxpt("identifier that is the loop variable");} |
	IDENT '=' error {yyxpt("indexing expression");} |
	error {yyxpt("identifier or assignment (id = expr) after 'for' ");}
;

whileStatement:
	WHILE expr optionalEndStatement statementList END {
	  $$ = $1; $$->addChild($2); $$->addChild($4);
	}  |
	WHILE error {yyxpt("test expression after 'while'");} 
;

ifStatement:
	IF conditionedStatement elseIfBlock elseStatement END {
	  $$ = $1; $$->addChild($2); if ($3 != NULL) $$->addChild($3); 
	  if ($4 != NULL) $$->addChild($4);
	} |
	IF error {yyxpt("condition expression for 'if'");}
;

conditionedStatement:
	expr optionalEndStatement statementList {
	  $$ = new AST(OP_CSTAT,$1,$3,$1->context());
	}
;

elseIfBlock:
	/* empty */ {$$ = NULL;} |
	elseIfStatementList
	;

elseIfStatementList:
	elseIfStatement {
	  $$ = new AST(OP_ELSEIFBLOCK,$1,$1->context());
	} |
	elseIfStatementList elseIfStatement {
	  $$ = $1; $$->addChild($2);
	}
;

elseIfStatement:
	ELSEIF conditionedStatement {
	  $$ = $2;
	} |
	ELSEIF error {yyxpt("test condition for 'elseif' clause");};

elseStatement:
	ELSE statementList {
	  $$ = $2;
	} |
	/* empty */ {$$ = NULL;} |
	ELSE error {yyxpt("statement list for 'else' clause");}
;

assignmentStatement:
	symbRefList '=' expr {$$ = new AST(OP_ASSIGN,$1,$3,(int)$2);} |
	symbRefList '=' error {yyxpt("expression in assignment");}
        ;

multiFunctionCall:
	'[' matrixDef ']' '='  IDENT '('  indexList ')' {
	  $5->addChild(new AST(OP_PARENS,$7,(int)$6));
	  $$ = new AST(OP_MULTICALL,$2,$5,(int)$1);
	} |
	'[' matrixDef ']' '=' IDENT  {
	  $5->addChild(new AST(OP_PARENS,NULL,-1));
	  $$ = new AST(OP_MULTICALL,$2,$5,(int)$1);
	} |
	'[' matrixDef ']' '='  IDENT '('  indexList error 
	{yyxpt("matching right parenthesis");} |
	'[' matrixDef ']' '='  IDENT '('  error 
	{yyxpt("indexing list");} |
	'[' matrixDef ']' '='  IDENT error 
	{yyxpt("left parenthesis");} |	
	'[' matrixDef ']' '=' error 
	{yyxpt("identifier");}
        ; 

expr:
	expr ':' expr {$$ = new AST(OP_COLON,$1,$3,(int)$2);} 
	| expr ':' error {yyxpt("an expression after ':'");}
	| terminal
	| expr '+' expr {$$ = new AST(OP_PLUS,$1,$3,(int)$2);}
	| expr '+' error {yyxpt("an expression after '+'");}
	| expr '-' expr {$$ = new AST(OP_SUBTRACT,$1,$3,(int)$2);}
	| expr '-' error {yyxpt("an expression after '-'");}
	| expr '*' expr {$$ = new AST(OP_TIMES,$1,$3,(int)$2);}
	| expr '*' error {yyxpt("an expression after '*'");}
	| expr '/' expr {$$ = new AST(OP_RDIV,$1,$3,(int)$2);}
	| expr '/' error {yyxpt("an expression after '/'");}
	| expr '\\' expr {$$ = new AST(OP_LDIV,$1,$3,(int)$2);}
	| expr '\\' error {yyxpt("an expression after '\\'");}
	| expr '|' expr {$$ = new AST(OP_OR,$1,$3,(int)$2);}
	| expr '|' error {yyxpt("an expression after '|'");}
	| expr '&' expr {$$ = new AST(OP_AND,$1,$3,(int)$2);}
	| expr '&' error {yyxpt("an expression after '&'");}
	| expr '<' expr {$$ = new AST(OP_LT,$1,$3,(int)$2);}
	| expr '<' error {yyxpt("an expression after '<'");}
	| expr LE  expr {$$ = new AST(OP_LEQ,$1,$3,(int)$2);}
	| expr LE  error {yyxpt("an expression after '<='");}
	| expr '>' expr {$$ = new AST(OP_GT,$1,$3,(int)$2);}
	| expr '>' error {yyxpt("an expression after '>'");}
	| expr GE  expr {$$ = new AST(OP_GEQ,$1,$3,(int)$2);}
	| expr GE  error {yyxpt("an expression after '>='");}
	| expr EQ  expr {$$ = new AST(OP_EQ,$1,$3,(int)$2);}
	| expr EQ  error {yyxpt("an expression after '=='");}
	| expr NE  expr {$$ = new AST(OP_NEQ,$1,$3,(int)$2);}
	| expr NE  error {yyxpt("an expression after '~='");}
	| expr DOTTIMES expr {$$ = new AST(OP_DOT_TIMES,$1,$3,(int)$2);}
	| expr DOTTIMES error {yyxpt("an expression after '.*'");}
	| expr DOTRDIV  expr {$$ = new AST(OP_DOT_RDIV,$1,$3,(int)$2);}
	| expr DOTRDIV  error {yyxpt("an expression after './'");}
	| expr DOTLDIV expr {$$ = new AST(OP_DOT_LDIV,$1,$3,(int)$2);}
	| expr DOTLDIV error {yyxpt("an expression after '.\\'");}
	| '-'  expr %prec NEG {$$ = new AST(OP_NEG,$2,(int)$1);}
	| '+'  expr %prec POS {$$ = $2;}
	| '~' expr %prec NOT {$$ = new AST(OP_NOT,$2,(int)$1);}
	| '~' error %prec NOT {yyxpt("an expression after logical not");}
	| expr '^' expr  {$$ = new AST(OP_POWER,$1,$3,(int)$2);}
	| expr '^' error  {yyxpt("an expression after '^'");}
	| expr DOTPOWER expr {$$ = new AST(OP_DOT_POWER,$1,$3,(int)$2);}
	| expr DOTPOWER error {yyxpt("an expression after '.^'");}
	| expr '\''          {$$ = new AST(OP_TRANSPOSE,$1,(int)$2);}
	| expr DOTTRANSPOSE  {$$ = new AST(OP_DOT_TRANSPOSE,$1,(int)$2);}
	| '(' expr ')'  {$$ = $2;}
	| '(' expr error  {yyxpt("a right parenthesis after expression");}
	| '(' error  {yyxpt("an expression after left parenthesis");}
	;

terminal:
        NUMERIC
	| STRING
	| MAGICEND
	| symbRefList {$$ = new AST(OP_RHS,$1,$1->context());}
        | '[' matrixDef ']' {$$ = $2;}
        | '[' error {yyxpt("a matrix definition followed by a right bracket");}
        | '[' rowSeperator matrixDef ']' {$$ = $3;}
        | '[' matrixDef rowSeperator ']' {$$ = $2;}
        | '[' rowSeperator matrixDef rowSeperator ']' {$$ = $3;}
	| '[' ']' {$$ = new AST(OP_EMPTY,NULL,(int)$1);}
        | '{' cellDef '}' {$$ = $2;}
        | '{' rowSeperator cellDef '}' {$$ = $3;}
        | '{' cellDef rowSeperator '}' {$$ = $2;}
        | '{' rowSeperator cellDef rowSeperator '}' {$$ = $3;}
	| '{' '}' {$$ = new AST(OP_EMPTY_CELL,NULL,(int)$1);}
	| '{' error {yyxpt("a cell-array definition followed by a right brace");}
        ;
symbRefList:
	IDENT 
        | symbRefList symbRef {$$ = $1; $$->addChild($2);}
        ;
	
symbRef:
	'(' indexList ')' {$$ = new AST(OP_PARENS,$2,(int)$1); }
	| '(' indexList error {yyxpt("matching right parenthesis");}
        | '{' indexList '}' {$$ = new AST(OP_BRACES,$2,(int)$1); }
	| '{' indexList error {yyxpt("matching right brace");}
        | '.' IDENT {$$ = new AST(OP_DOT,$2,(int)$1); }
	| '.' '(' expr ')' {$$ = new AST(OP_DOTDYN,$3,(int)$1);}
        ;

indexElement:
	expr
	| ':' {$$ = new AST(OP_ALL,NULL,(int)$1);}
	| '/' IDENT '=' expr {$$ = new AST(OP_KEYWORD,$2,$4,(int)$1);}
	| '/' IDENT '=' error {yyxpt("expecting expression after '=' in keyword assignment");}
	| '/' IDENT {$$ = new AST(OP_KEYWORD,$2,(int)$1);}
	| '/' error {yyxpt("expecting keyword identifier after '/' in keyword assignment");}
        ;

indexList:
	indexElement | 
        indexList ',' indexElement {$$ = $1; $$->addPeer($3);}
        ;

cellDef:
       rowDef {$$ = new AST(OP_BRACES,$1,$1->context());}|
       cellDef rowSeperator rowDef {$$ = $1; $$->addChild($3);}
       ;

matrixDef:
       rowDef {$$ = new AST(OP_BRACKETS,$1,$1->context());}|
       matrixDef rowSeperator rowDef {$$ = $1; $$->addChild($3);} 
       ;

rowSeperator:
       ENDSTMNT | ENDQSTMNT
       ;

columnSep:
       ',' | '#'
       ;

rowDef:
       expr {$$ = new AST(OP_SEMICOLON,$1,$1->context());} |
       rowDef  columnSep expr {$$ = $1; $$->addChild($3);}
       ;

%%

namespace FreeMat {
  
  void resetParser() {
    mainAST = NULL;
    mainMDef = NULL;
    errorOccured = false;
  }
  
  ASTPtr getParsedScriptBlock() {
    return mainAST;
  }
  
  MFunctionDef* getParsedFunctionDef() {
    return mainMDef;
  }
  
  ParserState parseState() {
    if (errorOccured)
      return ParseError;
    if (mainAST != NULL) 
      return ScriptBlock;
    else
      return FuncDef;
  }
  
  ParserState parseString(char *txt) {
/*     yydebug = 1; 	*/
    resetParser();
    interactiveMode = true;
    setLexBuffer(txt);
    yyparse();
    return parseState();
  }
  
  ParserState parseFile(FILE *fp, const char* fname) {
    resetParser();
    interactiveMode = false;
    parsing_filename = fname;
    setLexFile(fp);
    yyparse();
    return parseState();
  }
  
}
